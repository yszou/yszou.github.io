Erlang学习笔记
邹业盛
%%mtime(%Y-%m-%d %H:%M)
%!encoding: utf-8
%!options(xhtml): --google-analytics --disqus
%%toc


#交互环境#

``erl`` 是 Erlang 的交互终端.

Erlang 的每一个语句是 ``.`` 结束:

```text
1> 1 + 1.
2
2> 
```



=数据类型=

==整数==

```erlang
−234 0 10 100000000
2#1010 −16#EA
```

ASCII 字符可以当整数用：

```erlang
$a
$b
$\n
```


==浮点数==

```erlang
17.368 −56.654 1.234E-10
```


==原子==

类似于静态量的类型。

如果以小写字母开头，则可以使用字母，数字， //@// ， //.// ， //_// ：

```erlang
january fooBar alfa21 start_with_lower_case node@ramone true false
```

如果直接以单引号包起来，则可以使用任意的字符：

```erlang
'January' 'a space' 'Anything inside quotes{}#@ \n\012'
'node@ramone.erlang-consulting.com'
```


==布尔量==

Erlang 中没有单独的布尔类型，使用原子 //true// 和 //false// 来代替使用：

```erlang
true false
```


==元组==

各种成员的组合就是元组，使用大括号包起来，各成员以逗号分割：

```erlang
{123, bcd} {123, def, abc} {abc, {def, 123}, ghi} {}
{person, 'Joe', 'Armstrong'} {person, 'Mike', 'Williams'}
```


==列表==

各种成员的有序组合是列表，使用中括号包起来，各成员以逗号分割：

```erlang
[january, february, march]
[123, def, abc]
[a,[b,[c,d,e],f], g]
[]
[{person, 'Joe', 'Armstrong'}, {person, 'Robert', 'Virding'}, {person, 'Mike', 'Williams'}]
[72,101,108,108,111,32,87,111,114,108,100]
[$H,$e,$l,$l,$o,$ ,$W,$o,$r,$l,$d]
"Hello World"
```


==字符串==

字符串实际上就是整数列表，或者反过来说整数列表就是字符串(至少 ASCII 范围内)：

```erlang
1> "孤高天使".
[23396,39640,22825,20351]

2> [64,64,64,64].
"@@@@"
```

=运算符=

==算术运算符==

| + | 加(正) |
| - | 减法(负) |
| * | 乘法 |
| / | 除法 |
| div | 整除 |
| rem | 求余 |


==逻辑运算符==

| and | 与 |
| or | 或 |
| not | 非 |
| andalso | 与(短路) |
| orelse | 或(短路) |
| xor | 异或 |


==比较运算符==

| == | 等于 |
| /= | 不等于 | 
| =:= | 全等 |
| =/= | 不全等 |
| =< | 等于或小于 |
| < | 小于 |
| >= | 等于或大于 |
| > | 大于 |


不同类型之间不能按值比较，只遵循以下规则：

```erlang
number < atom < reference < fun < port < pid < tuple < list < binary
```

列表的比较根据成员的字典序进行：

```erlang
5> [boo,hoo]<[adder,zebra,bee].
false
6> [boo,hoo]<[boo,hoo,adder,zebra,bee].
true
```

元组的比较首先比较成员个数，再按列表规则比较成员：

```erlang
7> {boo,hoo}<{adder,zebra,bee}.
true
8> {boo,hoo}<{boo,hoo,adder,zebra,bee}.
true
```

全等会同时检查类型，它通常更快，不过用得不多：

```erlang
1> 1.0 == 1.
true
2> 1.0 =:= 1.
false
5> 1 =/= 1.0.
true
```


=列表结构=

列表可以以一种方便的语法切割成 //head// 和 //tail// 两部分，其中 //tail// 总是一个列表(可以是空列表)：

```erlang
List = [Element | List] or []
[1,2,3] == [1 | [2 | [3 | []]]]
```


=变量=

变量以大写字母开头，可以包含字母，数字，下划线。并且， Erlang 中某个变量只能被赋值一次，所有的变量传递都是值传递，作为函数参数时，变量先求得值再会去执行函数体。

在 Erlang shell 中可以使用 //f()// 和 //f(Var)// 取消所有或指定变量：

```erlang
f()
f(Double)
```


=模式匹配=

Erlang 中的模式匹配机制可以做的事有：

- 变量赋值
- 分支检查
- 展开数据


当一个模式匹配被执行时：

- 如果匹配成功，则相应变量被赋值
- 如果匹配失败，则没有变量会改变


```erlang
1> List = [1,2,3,4].
[1,2,3,4]

2> [Head|Tail] = List.
[1,2,3,4]

3> Head.
1

4> Tail.
[2,3,4]

5> [A, B, C] = [1, 2, 3]
[1,2,3]

6> A.
1

7> [A, B, D] = [1,2,4].
[1,2,4]

8> D.
4
```

复杂一点：

```erlang
{A, _, [B|_], {B}} = {abc, 23, [22, 23], {22}}
```


其中的 //_// 表示无关量，它可以当变量用，但永远不会被赋值，也就可以重复用了。


=模块与函数=

之所有把函数和模块放到一起说，是因为在 Erlang shell 中是无法直接定义一个函数的，你必须地单独的文件中定义函数，然后在 shell 中编译它，之后才能使用。

模块文件的文件名使用 //.erl// 作为扩展名，并且文件名要和模块名相同，比如 //demo.erl// ：

```erlang
-module(demo).
-export([double/1]).

%This is a comment.

double(Value) -> times(Value, 2).
times(X, Y) -> X * Y.
```

之后在 shell 中先使用 //cd()// 到达指定目录，就可以编译这个模块了：

```erlang
> cd('xxx').
> c(demo).
{ok,demo}
> demo:double(10).
20
```

模块文件中可以有一些使用 //-// 开头的选项，比如我们之前已经看到的：

```erlang
-module(demo).
-export([double/1]).
```

其它的比如还有： //import// 导入函数：

```erlang
-import(math, [sqrt/1]).
hello(X) -> sqrt(X).
```

也可以使用任意的自定义选项，不过这些选项只能有一个参数，比如：

```erlang
-author(zys).
-date(Date).
```

模块的详细信息可以使用它的 //module_info/1// 函数获取：

```erlang
demo:module_info().
```

模块的 //vsn// 属性是它的版本标识，如果不指定，则自动以当前文件内容的 MD5 值来填充。



=独立单元与消息传递=


并行编程是 Erlang 最大的一个特点， 项目中存在很多的单元（ Process ），单元与单元之间只能通过消息来传递信息。单元之间互不依赖，可以很方便进行分布式扩展。

要产生一个单元，直接使用 //spawn// 来执行一个函数即可：

```erlang
-module(demo).
-export([echo/1]).

echo(Msg) ->
    io:format("~p\n", [Msg]).
```

编译模块之后执行：

```erlang
1> Pid = spawn(demo, echo, ["new"]).
```

能看到 //echo// 函数执行的结果。同时还有类似于 //<0.61.0>// 这样的单元号。

上面的过程，就是在一个新的单元中，执行了一个函数。

单元通过单元号来标识， Erlang shell 本身是运行一个单元当中的，当前环境也很多的单元在运行着。可以使用 //self()// 来获取当前单元号。通过 //processes()// 查看当前单元列表。

```erlang
1> self().
<0.35.0>

2> processes().
[<0.0.0>,<0.3.0>,<0.5.0>,<0.6.0>,<0.8.0>,<0.9.0>,<0.10.0>,
 <0.11.0>,<0.12.0>,<0.13.0>,<0.14.0>,<0.15.0>,<0.16.0>,
 <0.18.0>,<0.19.0>,<0.20.0>,<0.21.0>,<0.22.0>,<0.23.0>,
 <0.24.0>,<0.25.0>,<0.26.0>,<0.27.0>,<0.28.0>,<0.29.0>,
 <0.35.0>]
```

单元之间通过消息来传递信息，消息的传递有专门的一个语法支持：

```erlang
Pid ! "Hello".
```

这就表示向 //Pid// 这个单元传递一个字符串 //Hello// 。

传递过去的消息被保存在单元的“信箱”当中，使用 //receive// 语句来抽取信箱中的一条消息，然后进行模式匹配，做相应处理。

```erlang
-module(demo).
-export([start/0, daemon/0]).

start() ->
    spawn(demo, daemon, []).

daemon() ->
    receive
        ok ->
            io:format("ok, exit.");
        Msg ->
            io:format("~p\n", [Msg]),
            daemon()
    end.
```

编译后执行：

```erlang
> Pid = demo:start().
<0.80.0>

> Pid ! "Haha".
"Haha"
"Haha"

> Pid ! ok.
ok, exit.ok
```

可以看到， ``Pid ! Msg`` 这个表达式的返回值是 //Msg// 。

//receive// 语句可以有一个超时值，使用 //after// ：

```erlang
-module(demo).
-export([timeout/0, never/0]).


timeout() ->
    receive
        Msg ->
            io:format("~p\n", [Msg])
    after 5000 ->
            io:format("timeout\n")
    end.


never() ->
    receive
        Msg ->
            io:format("~p\n", [Msg])
    after infinty ->
            io:format("timeout\n")
    end.
```

//infinty// 表示永不超时。



=并行单元的设计模式=

并行单元有三种常用的实现模式：

- Client/Server 模型，客户端/服务器。
- FSM 模型，有限状态机。
- Event Handler 模型，事件绑定机制。


==Client/Server==

这个模型将业务抽象成两种角色，服务器负责业务实现，客户端负责业务控制。然后两者以 请求/响应 的方式进行交互。在 Erlang 中的实现，就是把一些单元当成服务器用，再把一些单元当成客户端用。

以一个简单的远程函数调用为例，服务器：

```erlang
-module(server).
-export([start/0, daemon/0]).

start() ->
    spawn(server, daemon, []).

daemon() ->
    receive
        {Pid, A, B} ->
            Pid ! A + B,
            daemon();
        {Pid, quit} ->
            Pid ! ok
    end.
```

客户端：

```erlang
-module(client).
-export([connect/1, request/2, quit/0]).

connect(Pid) ->
    register(srv, Pid).

request(A, B) ->
    srv ! {self(), A, B},
    receive
        Result ->
            io:format("~p", [Result])
    end.

quit() ->
    srv ! {self(), quit},
    receive
        ok ->
            io:format("ok")
    end.
```

